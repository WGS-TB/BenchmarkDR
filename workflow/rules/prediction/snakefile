configfile: "config.yaml"

path_data = config["PATH_DATA"] + "/"
output_dir = config["OUTPUT_DIR"] + "/"

rule build_model:
    input:
        conf="workflow/rules/prediction/models_conf.yml"
    output:
        output_dir + "{bacterium}/prediction/models/{method}.joblib"
    conda:
        "../../envs/prediction/env.yml"
    shell:
        "python workflow/scripts/prediction/build_model.py --config {input.conf} --model-name {wildcards.method} --outfile {output}"

rule evaluation:
    input:
        data=path_data + "{bacterium}/representation/gene_presence_absence/0_matrix.csv",
        label=path_data + "{bacterium}/label_{drug}.csv",
        model=output_dir + "{bacterium}/prediction/models/{method}.joblib",
        conf="workflow/rules/prediction/models_conf.yml"
    output:
        output_dir + "{bacterium}/prediction/results/{method}-{drug}.csv"
    conda:
        "../../envs/prediction/env.yml"
    threads: 8
    script:
        "../../scripts/prediction/evaluation.py"

rule aggregate_summary:
    input:
        expand(output_dir + "{bacterium}/prediction/results/{method}-{drug}.csv",
            method=config["METHODS"], drug=config["DRUGS"], allow_missing=True)
    output:
        output_dir + "{bacterium}/prediction/summary.csv"
    script:
        "../../scripts/prediction/aggregate.py"

# TODO rule visual_report: